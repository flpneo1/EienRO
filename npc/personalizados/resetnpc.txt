//===== Hercules Script ======================================
//= Reset NPC
//===== By: ==================================================
//= Hercules Dev Team
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//============================================================

prontera,150,193,4	script	Garota Resetadora	533,{

	set .@ResetStat,10000000;	// Zeny for stat reset
	set .@ResetSkill,10000000;	// Zeny for skill reset
	set .@ResetBoth,18000000;	// Zeny for resetting both together

	mes "[Garota Resetadora]";
	mes "Olá "+strcharinfo(0)+"!";
	mes "Eu posso resetar seus pontos de status e habilidades.";
	mes "Resetar Status: 10 milhões de Zenys";//+ .@ResetStat +"z";
	mes "Resetar Skills: 10 milhões de Zenys";//+ .@ResetSkill +"z";
	mes "Resetar Ambos: 18 milhões de Zenys";//+ .@ResetBoth +"z";
	mes "Por favor, selecione o serviço que deseja:";
	next;
	switch(select("^FF3355Resetar Skills:Resetar Status:Resetar Ambos^000000:Cancelar")) {
	case 1:
		mes "[Garota Resetadora]";
		if (Zeny < .@ResetSkill) {
			mes "Desculpe, você não tem a quantidade de Zeny necessária.";
			close;
		}
		set Zeny, Zeny-.@ResetSkill;
		sc_end SC_ALL;
		resetskill;
		set contallskill,0;
		mes "Está pronto!";
		close;
	case 2:
		mes "[Garota Resetadora]";
		if (Zeny < .@ResetStat) {
			mes "Desculpe, você não tem a quantidade de Zeny necessária.";
			close;
		}
		set Zeny, Zeny-.@ResetStat;
		resetstatus;
		mes "Está pronto!";
		close;
	case 3:
		mes "[Garota Resetadora]";
		if (Zeny < .@ResetBoth) {
			mes "Desculpe, você não tem a quantidade de Zeny necessária.";
			close;
		}
		set Zeny, Zeny-.@ResetBoth;
		sc_end SC_ALL;
		resetskill;
		set contallskill,0;
		resetstatus;
		mes "Está pronto!";
		close;
	case 4:
		close;
	}
}
